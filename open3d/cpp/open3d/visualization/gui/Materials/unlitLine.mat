// Shader for rendering lines with width
//
material {
    name : unlitLine,
    shadingModel : unlit,
    depthWrite : true,
    depthCulling : true,
    doubleSided : true,
    vertexDomain: world,

    parameters : [
        { type : float4,    name : baseColor },
        { type : float4,    name : emissiveColor },
        { type : float,     name : lineWidth }
    ],
    requires: [
        color, custom0
    ]
}

vertex {
    void materialVertex(inout MaterialVertexInputs material) {
        float4 resolution = getResolution();
        float aspect = resolution.x / resolution.y;
        float4 pos = getPosition();
        // next.xyz is the position of the end of the line used to compute the direction of
        // the line.
        // next.w is the sign of the direction perpendicular to the line along which the
        // vertex will be moved to create the line's width.
        float4 next = getCustom0();

        // Project current vertex and next vertex to screen
        float4 world_pos = getWorldFromModelMatrix() * pos;
        float4 world_pos_next = getWorldFromModelMatrix() * float4(next.xyz, 1.0);
        float4 projected_pos = getClipFromWorldMatrix() * world_pos;
        float4 projected_next = getClipFromWorldMatrix() * world_pos_next;
        float2 screen_pos = projected_pos.xy / projected_pos.w;
        float2 screen_next = projected_next.xy / projected_next.w;

        // now compute offset for the desired line width
        float2 dir = normalize(screen_next - screen_pos);
        float4 normal = float4(-dir.y, dir.x, 0.0, 1.0);
        normal.xy *= (materialParams.lineWidth * resolution.z * 0.5 * aspect);
        normal *= getClipFromViewMatrix();
        normal.xy *= projected_pos.w;

        // Now project back to World space since Filament requires it for 'world' vertexDomain
        projected_pos.xy += normal.xy * next.w;
        material.worldPosition = getWorldFromClipMatrix() * projected_pos;
    }
}

fragment {
    void material(inout MaterialInputs material) {
        prepareMaterial(material);

        float combined_alpha = getColor().a * materialParams.baseColor.a;
        material.baseColor.rgb = materialParams.baseColor.rgb * getColor().rgb * combined_alpha;
        material.baseColor.a = combined_alpha;
        material.emissive = materialParams.emissiveColor;
}
}
